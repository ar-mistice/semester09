#  This file is part of Delaunay triangulation robust implementation.
#
#  Copyright (C) 2010, 2011  Vladimir Rutsky <altsysrq@gmail.com>
#
#  This program is free software: you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program.  If not, see <http://www.gnu.org/licenses/>.

import os.path
import itertools
import re

Import('global_env')
env = global_env.Clone()

env.AppendUnique(
    LIBPATH=['#boost-lib'],
    LIBS=['boost_unit_test_framework'])

wo_cgal_cpp_defines = list(set(env['CPPDEFINES']) - 
            set(['HAVE_CGAL', 'USE_CGAL_ON_INEXACT']))
wo_cgal_env = env.Clone(CPPDEFINES=wo_cgal_cpp_defines)

main_file = 'unit_testing.cpp'
source_files = set(Glob("*.cpp", strings=True)) - set([main_file])
objects = [
    env.Object(main_file)]
wo_cgal_objects = [
    wo_cgal_env.Object(re.sub(r'\.cpp$', '_wo_cgal', main_file), main_file)]
for source_file in source_files:
    wo_cgal_objects.append(wo_cgal_env.Object(
        re.sub(r'\.cpp$', '_wo_cgal', source_file), source_file))
    objects.append(env.Object(source_file))
    
global_env.unit_tests = \
    env.Program(target="unit_tests",
        source=objects)
global_env.unit_tests_wo_cgal = \
    wo_cgal_env.Program(target="unit_tests_wo_cgal",
        source=wo_cgal_objects)

run_tests = Command(
    ".passed", 
    global_env.unit_tests, 
    "{tests} && date -R > $TARGET".\
        format(tests=str(global_env.unit_tests[0].abspath)))
run_tests_wo_cgal = Command(
    ".passed_wo_cgal", 
    global_env.unit_tests_wo_cgal, 
    "{tests} && date -R > $TARGET".\
        format(tests=str(global_env.unit_tests_wo_cgal[0].abspath)))

# vim: set filetype=python ts=4 sw=4 et:
